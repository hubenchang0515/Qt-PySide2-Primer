# 在QML中使用Python对象
QML只负责UI界面的绘制工作，软件的业务逻辑应该由C++或Python等其他语言实现。  

要把Python对象注册到QML的上下文中，则该对象的类必须是QObject的子类，下面实现了一个简单的文件IO类 :  
```Python
class MyQmlFile(QObject) :
    def __init__(self, parent=None) :
        super().__init__(parent)
        
    @QtCore.Slot(str,str)
    def write(self, url, text) :
        # 打开文件时需要将url转换为本地路径，编码采用UTF-8
        fp = open(QUrl(url).toLocalFile(), "w", encoding='UTF-8')
        fp.write(text)
        fp.close()
        
    @QtCore.Slot(str, result=str)
    def read(self, url) :
        fp = open(QUrl(url).toLocalFile(), "r", encoding='UTF-8')
        ret = fp.read()
        fp.close()
        return ret
```
* 如果要在QML中使用对象的方法，则方法必须定义为槽，否则不能访问。

将这个类实例化为一个对象，并将对象注册到QML中，即可在QML中进行文件IO :  
```Python
if __name__ == "__main__":
    app = QGuiApplication(sys.argv)
    engine = QQmlApplicationEngine()

    # 实例化为对象
    myQmlFile = MyQmlFile()

    # 先将对象注册到QML上下文中，第一个参数为其在QML中的名字
    engine.rootContext().setContextProperty("file", myQmlFile)

    # 然后再加载QML文件
    engine.load("mainwindow.qml")

    sys.exit(app.exec_())
```

> 这里不能在注册的时候创建临时对象，因为QML引擎不会将该对象的引用计数加一，Python的GC会将该对象回收。
> ```Python
> if __name__ == "__main__":
>    app = QGuiApplication(sys.argv)
>    engine = QQmlApplicationEngine()
>
>    # 这样做会出错
>    engine.rootContext().setContextProperty("file", MyQmlFile())
>
>    # 然后再加载QML文件
>    engine.load("mainwindow.qml")
>
>    sys.exit(app.exec_())
>```

打开和保存文件的时候，需要一个选择文件的窗口，可以使用`QtQuick.Dialogs 1.3`中的`FileDialog` :  
```QML
FileDialog {
    id: 
    title:        // 窗口标题
    folder:       // 初始路径

    onAccepted:   // 确认选择文件的信号处理器
    onRejected:   // 点击取消的信号处理器

    fileUrl:      // 选中的文件的URL
}
```
* 通过`open`和`close`方法来打开和关闭该窗口

下面代码实现了一个简单的文本文件编辑器 :  

* `main.py`

```Python
import sys,os
from PySide2 import QtCore
from PySide2.QtCore import QObject, QUrl
from PySide2.QtGui import QGuiApplication
from PySide2.QtQml import QQmlApplicationEngine

class MyQmlFile(QObject) :
    def __init__(self, parent=None) :
        super().__init__(parent)
        
    @QtCore.Slot(str,str)
    def write(self, url, text) :
        # 打开文件时需要将url转换为本地路径，编码采用UTF-8
        fp = open(QUrl(url).toLocalFile(), "w", encoding='UTF-8')
        fp.write(text)
        fp.close()
        
    @QtCore.Slot(str, result=str)
    def read(self, url) :
        fp = open(QUrl(url).toLocalFile(), "r", encoding='UTF-8')
        ret = fp.read()
        fp.close()
        return ret

if __name__ == "__main__":
    app = QGuiApplication(sys.argv)
    engine = QQmlApplicationEngine()

    # 实例化为对象
    myQmlFile = MyQmlFile()

    # 先将对象注册到QML上下文中，第一个参数为其在QML中的名字
    engine.rootContext().setContextProperty("file", myQmlFile)

    # 然后再加载QML文件
    qmlFile = "mainwindow.qml"
    qmlPath = os.path.abspath(os.path.join(os.path.dirname(__file__), qmlFile))
    engine.load(qmlPath)

    sys.exit(app.exec_())
```

* `mainwindow.qml`

```QML
import QtQuick 2.13
import QtQuick.Controls 2.5
import QtQuick.Dialogs 1.3

ApplicationWindow {
    id: mainWindow
    visible: true // 是否可见，请注意默认值是false不可见
    width: 640
    height: 400
    title: qsTr("PySide2 QML")

    // FileDialog是用于选择文件的窗口
    FileDialog {
        id: openFileDialog
        title: qsTr("打开文件")
        folder: "."
        onAccepted: {
            // 使用从Python注册的file对象读取文件内容
            editor.text = file.read(fileUrls)

            // 关闭这个窗口
            close() 
        }
    }

    FileDialog {
        id: saveFileDialog
        title: qsTr("保存文件")
        folder: "."
        onAccepted: {
            // 使用从Python注册的file对象保存文件内容
            file.write(fileUrls, editor.text)

            // 关闭这个窗口
            close()
        }
    }

    // 打开文件的Action
    Action {
        id: openFileAction

        // 触发action时显示文件窗口
        onTriggered: {
            openFileDialog.open()
        }
    }

    // 保存文件的Action
    Action {
        id: saveFileAction

        onTriggered: {
            saveFileDialog.open()
        }
    }

    // 复制的Action
    Action {
        id: copyAction

        onTriggered: {
            editor.copy() // 调用editor的copy方法
            editor.forceActiveFocus() // 恢复焦点
        }

    }

    // 粘贴的Action
    Action {
        id: pasteAction

        onTriggered: {
            editor.paste() // 调用editor的paste方法
            editor.forceActiveFocus() // 恢复焦点
        }
    }

    // 状态栏
    menuBar : MenuBar {
        Menu {
            title: qsTr("文件(&F)")
            MenuItem { 
                action: openFileAction
                text: qsTr("打开(&O)")
            }
            MenuItem { 
                action: saveFileAction
                text: qsTr("保存(&S)")
            }
        }

        Menu {
            title: qsTr("编辑(&E)")
            MenuItem { 
                action: copyAction
                text: qsTr("复制")
            }
            MenuItem {
                action: pasteAction
                text: qsTr("粘贴")
            }
        }
    }

    // Header工具栏
    header : ToolBar {
        Row {
            ToolButton {
                action: openFileAction
                icon.source: "./res/open.png"
                icon.color: "transparent"
            }

            ToolButton {
                action: saveFileAction
                icon.source: "./res/save.png"
                icon.color: "transparent"
            }

            ToolButton {
                action: copyAction 
                icon.source: "./res/copy.png"
                icon.color: "transparent"
            }

            ToolButton {
                action: pasteAction 
                icon.source: "./res/paste.png"
                icon.color: "transparent"
            }
        }
    }

    // Footer放置一个文本当作状态栏
    footer : Text {
        text: qsTr("状态栏")

    }

    // 其余对象属于Content
    ScrollView { 
        id: scrollView
        anchors.fill: parent
        focus: true

        property int select : 0

        TextArea {
            id: editor
            font.pixelSize: 22
            focus: true  // 获取焦点(即键盘输入状态)
            persistentSelection: true  // 丢失焦点时保留选取
            selectByMouse: true  // 允许通过鼠标选取

            // text发生改变时根据长宽判断是否需要显示滚动条
            onTextChanged : {
                if(editor.height > scrollView.height)
                {
                    scrollView.ScrollBar.vertical.policy = ScrollBar.AlwaysOn
                }
                else
                {
                    scrollView.ScrollBar.vertical.policy = ScrollBar.AlwaysOff
                }

                if(editor.width > scrollView.width)
                {
                    scrollView.ScrollBar.horizontal.policy = ScrollBar.AlwaysOn
                }
                else
                {
                    scrollView.ScrollBar.horizontal.policy = ScrollBar.AlwaysOff
                }
            }
        }
    }
}
```

![Image](/image/qml/06.python_object/editor.png)

## 参考 :  
* QML
  * [QUrl](https://doc.qt.io/qtforpython/PySide2/QtCore/QUrl.html)
  * [QQmlEngine](https://doc.qt.io/qtforpython/PySide2/QtQml/QQmlEngine.htm)
  * [QQmlContext](https://doc.qt.io/qtforpython/PySide2/QtQml/QQmlContext.html)

* PySide2
  * [FileDialog](https://doc.qt.io/qt-5/qml-qtquick-dialogs-filedialog.html)