# 在QML中使用信号槽
[Qt信号槽机制](/note/basic/02.signal_and_slot.md)在前文中已经介绍过，本小节介绍QML中的信号槽机制。  

在QML里，信号和槽必须遵守驼峰命名法，名为`signalName`的信号，自动连接到相同元素中名为`onSignalName`的槽函数(也称为`信号处理器`，采用[QML扩展的JavaScript](https://doc.qt.io/qt-5/qtqml-javascript-topic.html))。  
例如`Button`元素有如下七个信号和对应的槽函数 :  

| 信号              | 槽                | 说明                                                        |
| :-:               | :-:               | :-                                                         |
| `canceled`        | `onCanceled`      | 在按钮上按下鼠标左键后，将鼠标移出按钮后松开时发送该信号        |
| `clicked`         | `onClicked`       | 在按钮上单击鼠标左键时发送该信号                     |
| `doubleClicked`   | `onDoubleClicked` | 在按钮上双击鼠标左键时发送该信号                              |
| `pressAndHold`    | `onPressAndHold`  | 在按钮上长按鼠标左键时发送该信号                              |
| `pressed`         | `onPressed`       | 在按钮上按下鼠标左键时发送该信号                              |
| `released`        | `onReleased`      | 在按钮上松开鼠标左键时发送该信号                              |
| `toggled`         | `onToggled`       | 当`checkable`按钮切换状态时发送该信号                        |

下面代码在点击按钮时，会随机改变`rect`的颜色 :  

```QML
import QtQuick 2.13
import QtQuick.Window 2.13
import QtQuick.Controls 2.5
import QtQuick.Layouts 1.13

Window {
    id: mainWindow
    visible: true
    width: 640
    height: 400
    title: qsTr("PySide2 QML")

    ColumnLayout {
        anchors.fill: parent

        Rectangle {
            id: rect
            Layout.fillWidth: true
            Layout.preferredHeight: 300
            color: "black"
        }

        Button {
            width: 640
            text: qsTr("BUTTON")
            Layout.fillWidth: true
            Layout.fillHeight: true
            onClicked : {
                // 将rect的颜色设为随机值
                rect.color = Qt.rgba(Math.random(),Math.random(),Math.random(),1)
            }
        }
    }
}
```
![Image](/image/qml/02.qml_signal_and_slot/button.png)

## 自定义信号
在QML中，可以通过`signal`关键字来定义信号，下面代码在为`rect`定义了名为`colorChanged`的信号和与其连接的`onColorChanged`槽，功能和之前一样，也是点击按钮时随机修改`rect`的颜色 :  
```QML
import QtQuick 2.13
import QtQuick.Window 2.13
import QtQuick.Controls 2.5
import QtQuick.Layouts 1.13

Window {
    id: mainWindow
    visible: true
    width: 640
    height: 400
    title: qsTr("PySide2 QML")

    ColumnLayout {
        anchors.fill: parent

        Rectangle {
            id: rect
            Layout.fillWidth: true
            Layout.preferredHeight: 300
            color: "black"

            // 定义信号
            signal colorChanged(double r, double g, double b)
            // 对应的槽
            onColorChanged: function(r, g, b) {
                rect.color = Qt.rgba(r, g, b, 1)
            }
        }

        Button {
            width: 640
            text: qsTr("BUTTON")
            Layout.fillWidth: true
            Layout.fillHeight: true
            onClicked : {
                // rect发送colorChanged信号
                rect.colorChanged(Math.random(), Math.random(), Math.random())
            }
        }
    }
}
```

## 连接信号  
使用`Connections`元素(需要`import QtQml 2.13`)，可以将信号连接到元素之外的信号处理器，并且可以连接多个信号处理器。
```QML
Connections {
    target:          // 信号发出者的id
    on<SignalName>:  // 信号处理器函数
}
```

下面代码和之前差不多，创建了两个Rectangle，在点击按钮时，两个Rectangle都会被随机更改颜色 :  
```QML
import QtQuick 2.13
import QtQuick.Window 2.13
import QtQuick.Controls 2.5
import QtQuick.Layouts 1.13
import QtQml 2.13

Window {
    id: mainWindow
    visible: true
    width: 640
    height: 400
    title: qsTr("PySide2 QML")

    ColumnLayout {
        anchors.fill: parent

        RowLayout {
            Layout.fillWidth: true
            Layout.preferredHeight: 300

            Rectangle {
                id: rect1
                Layout.fillWidth: true
                Layout.fillHeight: true
                color: "black"
                // 连接信号槽
                Connections {
                    target: button // 信号发出者的id
                    onClicked: {   // clicked信号的槽
                        rect1.color = Qt.rgba(Math.random(), Math.random(), Math.random(), 1)
                    }
                }
            }

            Rectangle {
                id: rect2
                Layout.fillWidth: true
                Layout.fillHeight: true
                color: "black"
                // 连接信号槽
                Connections {
                    target: button // 信号发出者的id
                    onClicked: {   // clicked信号的槽
                        rect2.color = Qt.rgba(Math.random(), Math.random(), Math.random(), 1)
                    }
                }
            }
        }

        Button {
            id: button
            width: 640
            text: qsTr("BUTTON")
            Layout.fillWidth: true
            Layout.fillHeight: true
        }
    }
}
```

![Image](/image/qml/02.qml_signal_and_slot/connect.png)

## 参考 :  
* QML
  * [AbstractButton](https://doc.qt.io/qt-5/qml-qtquick-controls2-abstractbutton.html)
  * [Button](https://doc.qt.io/qt-5/qml-qtquick-controls2-button.html)
  * [Connections](https://doc.qt.io/qt-5/qml-qtqml-connections.html)