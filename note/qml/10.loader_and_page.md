# 使用Loader变更页面
利用`Loader`加载qml文件的功能，可以方便的实现UI页面的一种变更方式

```QML
Loader {
    function setSource(source, properties)
}
```
* `source` 参数是要加载的文件名
* `properties` 是一个键值对列表，用于修改加载的顶级元素的属性值

例如 `setSource("demo.qml", {"title" : "sugar", "color" : "blue"})` 表示加载 `demo.qml` ，并将 `title` 属性的值改为 `"sugar"` ， `color` 属性的值改为 `"blue"` 。

下面是一个示例，实现了登录和退出的界面切换功能 :  

* `mainwindow.qml`  

```QML
import QtQuick 2.13
import QtQuick.Window 2.13
import QtQuick.Controls 2.5
import QtQuick.Layouts 1.13

Window {
    id: mainWindow
    visible: true
    width: 640
    height: 400
    title: qsTr("PySide2 QML")

    Loader {
        id: mainLoader
        anchors.fill: parent
        source: "page1.qml"
    }

    Connections {
        // 信号发送者，item是Loader的属性，表示所加载对象的顶级元素
        target: mainLoader.item 

        // 信号处理器
        onLogin: function(username, password){
            // 加载page2.qml，并设置username和password的值
            mainLoader.setSource("page2.qml",
                        { "username" : username,
                        "password" : password
                        })
        }
    }

    Connections {
        // 信号发送者，item是Loader的属性，表示所加载对象的顶级元素
        target: mainLoader.item 

        // 信号处理器
        onLogout: function(){
            mainLoader.setSource("page1.qml")
        }
    }
}
```

* `page1.qml`

```QML
import QtQuick 2.13
import QtQuick.Controls 2.5
import QtQuick.Layouts 1.13

Item {
    id: rootItem
    signal login(string username, string password)
    signal logout()

    GridLayout {
        anchors.centerIn: parent
        columns: 2

        Text {
            Layout.fillWidth: true
            text: qsTr("用户名")
            horizontalAlignment: Text.AlignHCenter
            verticalAlignment: Text.AlignVCenter
        }

        TextField {
            id: userInput
        }

        Text {
            Layout.fillWidth: true
            text: qsTr("密  码")
            horizontalAlignment: Text.AlignHCenter
            verticalAlignment: Text.AlignVCenter
        }

        TextField {
            id: passInput
            echoMode: TextInput.Password
        }

        Button {
            text: qsTr("登录")
            Layout.columnSpan: 2 // 占两列
            Layout.fillWidth: true

            onClicked: {
                rootItem.login(userInput.text, passInput.text)
            }
        }
    }
}
```

* `page2.qml`  

```QML
import QtQuick 2.13
import QtQuick.Controls 2.5
import QtQuick.Layouts 1.13

Item {
    id: rootItem
    signal login(string username, string password)
    signal logout()

    /* 定义两个属性用来接收参数 */
    property string username
    property string password

    ColumnLayout {
        anchors.centerIn: parent
    
        Text {
            text: "<h1>Welcome " + username + "</h1>"
        }

        Button {
            text: qsTr("退出")
            Layout.alignment: Qt.AlignHCenter // 布局块内水平居中

            onClicked: {
                rootItem.logout()
            }
        }

    }
}
```

下面是运行结果 :  
![login](/image/qml/10.loader_and_page/login.png)  
![welcome](/image/qml/10.loader_and_page/welcome.png)  